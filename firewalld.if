## <summary>policy for firewalld</summary>

########################################
## <summary>
##	Execute a domain transition to run firewalld.
## </summary>
## <param name="domain">
## <summary>
##	Domain allowed access.
## </summary>
## </param>
#
interface(`firewalld_domtrans',`
	gen_require(`
		type firewalld_t, firewalld_exec_t;
	')

	domtrans_pattern($1, firewalld_exec_t, firewalld_t)
')


########################################
## <summary>
##	Execute firewalld server in the firewalld domain.
## </summary>
## <param name="domain">
##	<summary>
##	The type of the process performing this action.
##	</summary>
## </param>
#
interface(`firewalld_initrc_domtrans',`
	gen_require(`
		type firewalld_initrc_exec_t;
	')

	init_labeled_script_domtrans($1, firewalld_initrc_exec_t)
')

########################################
## <summary>
##	Execute firewalld server in the firewalld domain.
## </summary>
## <param name="domain">
##	<summary>
##	Domain allowed to transition.
##	</summary>
## </param>
#
interface(`firewalld_systemctl',`
	gen_require(`
		type firewalld_t;
		type firewalld_unit_file_t;
	')

	systemd_exec_systemctl($1)
	allow $1 firewalld_unit_file_t:file read_file_perms;
	allow $1 firewalld_unit_file_t:service manage_service_perms;

	ps_process_pattern($1, firewalld_t)
')

########################################
## <summary>
##	Send and receive messages from
##	firewalld over dbus.
## </summary>
## <param name="domain">
##	<summary>
##	Domain allowed access.
##	</summary>
## </param>
#
interface(`firewalld_dbus_chat',`
	gen_require(`
		type firewalld_t;
		class dbus send_msg;
	')

	allow $1 firewalld_t:dbus send_msg;
	allow firewalld_t $1:dbus send_msg;
')

########################################
## <summary>
##	All of the rules required to administrate
##	an firewalld environment
## </summary>
## <param name="domain">
##	<summary>
##	Domain allowed access.
##	</summary>
## </param>
## <param name="role">
##	<summary>
##	Role allowed access.
##	</summary>
## </param>
## <rolecap/>
#
interface(`firewalld_admin',`
	gen_require(`
		type firewalld_t, firewalld_initrc_exec_t;
		type firewall_etc_rw_t, firewalld_var_run_t;
		type firewalld_var_log_t;
	')

	allow $1 firewalld_t:process signal_perms;
	ps_process_pattern($1, firewalld_t)
	tunable_policy(`deny_ptrace',`',`
		allow $1 firewalld_t:process ptrace;
	')

	firewalld_initrc_domtrans($1)
	domain_system_change_exemption($1)
	role_transition $2 firewalld_initrc_exec_t system_r;
	allow $2 system_r;

	files_search_pids($1)
	admin_pattern($1, firewalld_var_run_t)
	
	logging_search_logs($1)
	admin_pattern($1, firewalld_var_log_t)

	admin_pattern($1, firewall_etc_rw_t)

	admin_pattern($1, firewalld_unit_file_t)
	firewalld_systemctl($1)
	allow $1 firewalld_unit_file_t:service all_service_perms;
')
