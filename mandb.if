## <summary>On-line manual database.</summary>

########################################
## <summary>
##	Execute the mandb program in
##	the mandb domain.
## </summary>
## <param name="domain">
##	<summary>
##	Domain allowed to transition.
##	</summary>
## </param>
#
interface(`mandb_domtrans',`
	gen_require(`
		type mandb_t, mandb_exec_t;
	')

	corecmd_search_bin($1)
	domtrans_pattern($1, mandb_exec_t, mandb_t)
')

########################################
## <summary>
##	Execute mandb in the mandb
##	domain, and allow the specified
##	role the mandb domain.
## </summary>
## <param name="domain">
##	<summary>
##	Domain allowed to transition.
##	</summary>
## </param>
## <param name="role">
##	<summary>
##	Role allowed access.
##	</summary>
## </param>
#
interface(`mandb_run',`
	gen_require(`
		attribute_role mandb_roles;
	')

	lightsquid_domtrans($1)
	roleattribute $2 mandb_roles;
')

########################################
## <summary>
##	Search mandb cache directories.
## </summary>
## <param name="domain">
##	<summary>
##	Domain allowed access.
##	</summary>
## </param>
#
interface(`mandb_search_cache',`
	gen_require(`
		type mandb_cache_t;
	')

	files_search_var($1)
	allow $1 mandb_cache_t:dir search_dir_perms;
')

########################################
## <summary>
##	Delete mandb cache content.
## </summary>
## <param name="domain">
##	<summary>
##	Domain allowed access.
##	</summary>
## </param>
#
interface(`mandb_delete_cache_content',`
	gen_require(`
		type mandb_cache_t;
	')

	files_search_var($1)
	allow $1 mandb_cache_t:dir delete_dir_perms;
	allow $1 mandb_cache_t:file delete_file_perms;
	allow $1 mandb_cache_t:lnk_file delete_lnk_file_perms;
')

########################################
## <summary>
##	Read mandb cache content.
## </summary>
## <param name="domain">
##	<summary>
##	Domain allowed access.
##	</summary>
## </param>
#
interface(`mandb_read_cache_content',`
	gen_require(`
		type mandb_cache_t;
	')

	files_search_var($1)
	allow $1 mandb_cache_t:dir list_dir_perms;
	allow $1 mandb_cache_t:file read_file_perms;
	allow $1 mandb_cache_t:lnk_file read_lnk_file_perms;
')

########################################
## <summary>
##	Create, read, write, and delete
##	mandb cache files.
## </summary>
## <param name="domain">
##	<summary>
##	Domain allowed access.
##	</summary>
## </param>
#
interface(`mandb_manage_cache_content',`
	gen_require(`
		type mandb_cache_t;
	')

	files_search_var($1)
	allow $1 mandb_cache_t:dir manage_dir_perms;
	allow $1 mandb_cache_t:file manage_file_perms;
	allow $1 mandb_cache_t:lnk_file manage_lnk_file_perms;
')

########################################
## <summary>
##	All of the rules required to
##	administrate an mandb environment.
## </summary>
## <param name="domain">
##	<summary>
##	Domain allowed access.
##	</summary>
## </param>
## <param name="role">
##	<summary>
##	Role allowed access.
##	</summary>
## </param>
## <rolecap/>
#
interface(`mandb_admin',`
	gen_require(`
		type mandb_t, mandb_cache_t;
	')

	allow $1 mandb_t:process { ptrace signal_perms };
	ps_process_pattern($1, mandb_t)

	mandb_run($1, $2)

	files_search_var($1)
	admin_pattern($1, mandb_cache_t)
')
